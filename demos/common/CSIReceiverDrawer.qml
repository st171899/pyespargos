import QtQuick
import QtQuick.Controls
import QtQuick.Layouts
import QtQuick.Controls.Material
import "." as Common

Drawer {
	id: root

	property int headerHeight: 0
	property bool calibrationInProgress: false
	property real calibrationStart: 0.0
	property real calibrationProgress: 0.0
	readonly property int rfSwitchMode: rfSwitchInput.currentIndex
	readonly property bool tooltipsEnabled: ApplicationWindow.window ? ApplicationWindow.window.tooltipsEnabled : true
	readonly property string gainTooltipText: "RX gain is configured in 1 dB units. FFT gain is configured in 0.25 dB units."

	function updateCalibrationProgress() {
		if (!calibrationInProgress) {
			calibrationProgress = 0.0
			return
		}
		var elapsed = Math.max(0, (Date.now() / 1000) - calibrationStart)
		calibrationProgress = Math.min(1.0, elapsed / Math.max(0.001, poolConfigManager.getCachedValue("calibration.duration", 1.0)))
		if (calibrationProgress >= 1.0) {
			calibrationInProgress = false
		}
	}

	Timer {
		id: calibrationTimer
		interval: 20
		repeat: true
		running: root.calibrationInProgress
		onTriggered: root.updateCalibrationProgress()
		onRunningChanged: root.updateCalibrationProgress()
	}

	// Match app-wide Material settings
	Material.theme: Material.Dark
	Material.primary: "#227b3d"
	Material.accent: "#227b3d"
	Material.roundedScale: Material.notRounded

	implicitHeight: parent ? parent.height - headerHeight : 0
	y: headerHeight
	implicitWidth: 350
	edge: Qt.LeftEdge
	dragMargin: 50
	modal: false

	background: Rectangle {
		radius: 0
		color: "#222a2f"
	}

	ScrollView {
		anchors.fill: parent
		clip: false
		ScrollBar.vertical.visible: true
		anchors.leftMargin: 20
		anchors.rightMargin: 0
		anchors.topMargin: 0
		anchors.bottomMargin: 0

		GridLayout {
			Layout.alignment: Qt.AlignTop
			Layout.margins: 12
			columns: 2
			columnSpacing: 16
			rowSpacing: 10
			anchors.topMargin: 20
			anchors.bottomMargin: 20
			anchors.rightMargin: 20

			Label {
				Layout.columnSpan: 2
				text: "Receiver Settings"
				font.pixelSize: 18
				color: "#ffffff"
				horizontalAlignment: Text.AlignHCenter
				topPadding: 20
			}

			// Section: Channel
			Label { Layout.columnSpan: 2; text: "Channel"; color: "#9fb3c8" }
			Label { text: "Channel"; color: "#ffffff"; horizontalAlignment: Text.AlignRight; Layout.alignment: Qt.AlignRight; Layout.fillWidth: true }
			ComboBox {
				id: chanInput
				property string configKey: "channel"
				property string configProp: "currentIndex"
				// Note: Channel is in range 1-13, but index is in range 0-12
				property var encode: function(v) { return v + 1 }
				property var decode: function(v) { return Math.max(0, Math.min(12, parseInt(v||1)-1)) }
				Component.onCompleted: poolConfigManager.register(this)
				onCurrentIndexChanged: {
					if (poolConfigManager.onControlChanged(this))
						calibButton.needCalibration = true
				}
				implicitWidth: 180
				model: [ "1 (2.412 GHz)", "2 (2.417 GHz)", "3 (2.422 GHz)", "4 (2.427 GHz)", "5 (2.432 GHz)", "6 (2.437 GHz)", "7 (2.442 GHz)", "8 (2.447 GHz)", "9 (2.452 GHz)", "10 (2.457 GHz)", "11 (2.462 GHz)", "12 (2.467 GHz)", "13 (2.472 GHz)" ]
				currentIndex: 0
				function isUserActive() { return pressed || popup.visible }
			}

			Label { text: "Secondary"; color: "#ffffff"; horizontalAlignment: Text.AlignRight; Layout.alignment: Qt.AlignRight; Layout.fillWidth: true }
			ComboBox {
				id: secChanInput
				property string configKey: "secondary_channel"
				property string configProp: "currentIndex"
				property var encode: function(v) { return v }
				property var decode: function(v) { return Math.max(0, Math.min(3, parseInt(v||0))) }
				Component.onCompleted: poolConfigManager.register(this)
				onCurrentIndexChanged: {
					if (poolConfigManager.onControlChanged(this))
						calibButton.needCalibration = true
				}
				implicitWidth: 180
				model: [ "None", "Above", "Below" ]
				currentIndex: 0
				function isUserActive() { return pressed || popup.visible }
			}

			// Section: Calibration
			Label { Layout.columnSpan: 2; text: "Calibration"; color: "#9fb3c8" }
			Button {
				id: calibButton
				Layout.columnSpan: 2;
				Layout.alignment: Qt.AlignCenter;
				text: "Trigger Calibration";
				onClicked: {
					if (root.calibrationInProgress) return
					poolConfigManager.action("calibrate")
					needCalibration = false
					root.calibrationStart = Date.now() / 1000
					root.calibrationInProgress = true
					root.calibrationProgress = 0.0
				}
				property bool needCalibration: false

				// Make the complete action visually urgent after a channel change.
				Material.background: needCalibration ? "#7f1d1d" : root.Material.background
				Material.foreground: "#ffffff"
			}

			ProgressBar {
				Layout.columnSpan: 2
				Layout.fillWidth: true
				from: 0
				to: 1
				value: root.calibrationProgress
				visible: root.calibrationInProgress
			}

			Label { text: "Per Board"; color: "#ffffff"; horizontalAlignment: Text.AlignRight; Layout.alignment: Qt.AlignRight; Layout.fillWidth: true }
			Switch {
				id: perBoardCalibSwitch
				property string configKey: "calibration.per_board"
				property string configProp: "checked"
				property var encode: function(v) { return v ? 1 : 0 }
				property var decode: function(v) { return !!v }
				Component.onCompleted: poolConfigManager.register(this)
				onCheckedChanged: poolConfigManager.onControlChanged(this)
				checked: false
				ToolTip.visible: root.tooltipsEnabled && hovered
				ToolTip.text: "For multi-board setups: Calibrate each ESPARGOS board independently. Enable this when boards do not share one common clock and phase reference signal."
			}
			// Section: Signal Path
			Label { Layout.columnSpan: 2; text: "Signal Path / Format"; color: "#9fb3c8" }
			Label { text: "RF Switch"; color: "#ffffff"; horizontalAlignment: Text.AlignRight; Layout.alignment: Qt.AlignRight; Layout.fillWidth: true }
			ComboBox {
				id: rfSwitchInput
				property string configKey: "rf_switch"
				property string configProp: "currentIndex"
				property var encode: function(v) { return v }
				property var decode: function(v) { return Math.max(0, Math.min(4, parseInt(v||0))) }
				Component.onCompleted: poolConfigManager.register(this)
				onCurrentIndexChanged: poolConfigManager.onControlChanged(this)
				implicitWidth: 180
				model: [ "Isolated", "Reference", "45° Right", "45° Left", "Random" ]
				currentIndex: 0
				function isUserActive() { return pressed || popup.visible }
			}

			Label { text: "Show Reference"; color: "#ffffff"; horizontalAlignment: Text.AlignRight; Layout.alignment: Qt.AlignRight; Layout.fillWidth: true }
			Switch {
				id: showReferenceSwitch
				property string configKey: "show_reference"
				property string configProp: "checked"
				property var encode: function(v) { return v ? 1 : 0 }
				property var decode: function(v) { return !!v }
				Component.onCompleted: poolConfigManager.register(this)
				onCheckedChanged: poolConfigManager.onControlChanged(this)
				checked: false
				ToolTip.visible: root.tooltipsEnabled && hovered
				ToolTip.text: "Treat CSI received over the on-board reference channel as if it were over-the-air CSI and include it in the normal processing."
			}

			Label { text: "Force L-LTF"; color: "#ffffff"; horizontalAlignment: Text.AlignRight; Layout.alignment: Qt.AlignRight; Layout.fillWidth: true }
			Switch {
				id: forceAcquireLLTFSwitch
				property string configKey: "acquire_lltf_force"
				property string configProp: "checked"
				property var encode: function(v) { return v ? 1 : 0 }
				property var decode: function(v) { return !!v }
				Component.onCompleted: poolConfigManager.register(this)
				onCheckedChanged: poolConfigManager.onControlChanged(this)
				checked: false
				ToolTip.visible: root.tooltipsEnabled && hovered
				ToolTip.text: "Always acquire the legacy L-LTF CSI (20 MHz bandwidth), regardless of packet format. Useful when you want a common CSI format across mixed traffic, but it will prevent access to format-specific training fields."
			}

			Label { text: "L-LTF 8-Bit Mode"; color: "#ffffff"; horizontalAlignment: Text.AlignRight; Layout.alignment: Qt.AlignRight; Layout.fillWidth: true }
			Switch {
				id: lltf8BitModeSwitch
				property string configKey: "lltf_8bit_mode"
				property string configProp: "checked"
				property var encode: function(v) { return v ? 1 : 0 }
				property var decode: function(v) { return !!v }
				Component.onCompleted: poolConfigManager.register(this)
				onCheckedChanged: poolConfigManager.onControlChanged(this)
				checked: false
				ToolTip.visible: root.tooltipsEnabled && hovered
				ToolTip.text: "Use 8-bit L-LTF CSI with every subcarrier instead of sparse 12-bit L-LTF CSI."
			}

			Label { text: "Compressed CSI"; color: "#ffffff"; horizontalAlignment: Text.AlignRight; Layout.alignment: Qt.AlignRight; Layout.fillWidth: true }
			Switch {
				id: compressCSISwitch
				property string configKey: "compress_csi"
				property string configProp: "checked"
				property var encode: function(v) { return v ? 1 : 0 }
				property var decode: function(v) { return !!v }
				Component.onCompleted: poolConfigManager.register(this)
				onCheckedChanged: poolConfigManager.onControlChanged(this)
				checked: false
				ToolTip.visible: root.tooltipsEnabled && hovered
				ToolTip.text: "Compress CSI in firmware by converting it to a sparser time-domain representation before transport. This reduces bandwidth, but the received CSI is no longer the raw frequency-domain estimate."
			}

			// Section: Corrections
			Label { Layout.columnSpan: 2; text: "Corrections"; color: "#9fb3c8" }
			Label { text: "CFO correct."; color: "#ffffff"; horizontalAlignment: Text.AlignRight; Layout.alignment: Qt.AlignRight; Layout.fillWidth: true }
			ComboBox {
				id: cfoMode
				property string configKey: "cfo_compensation"
				property string configProp: "currentIndex"
				property var encode: function(v) { return v === 0 }
				property var decode: function(v) { return v ? 0 : 1 }
				Component.onCompleted: poolConfigManager.register(this)
				onActivated: poolConfigManager.onControlChanged(this)
				model: [ "Auto", "Manual" ]
				currentIndex: 0
				function isUserActive() { return pressed || popup.visible }
				ToolTip.visible: root.tooltipsEnabled && hovered
				ToolTip.text: "Auto estimates CFO for each packet. Manual uses the fixed value below; use 0 Hz for shared-clock reference or radar packets."
			}
			Label { text: "Manual CFO (Hz)"; color: "#ffffff"; Layout.alignment: Qt.AlignRight }
			SpinBox {
				id: cfoValue
				// Store hardware steps in increasing Hz order (the negative of NRXFOE).
				readonly property real hzPerStep: 80000000 / Math.pow(2, 20)
				property string configKey: "cfo_value_hz"
				property string configProp: "value"
				property var encode: function(v) { return v * hzPerStep }
				property var decode: function(v) { return -Math.round(-Number(v) / hzPerStep) }
				Component.onCompleted: poolConfigManager.register(this)
				onValueModified: poolConfigManager.onControlChanged(this)
				from: -4095
				to: 4096
				stepSize: 1
				value: 0
				editable: true
				enabled: cfoMode.currentIndex === 1
				textFromValue: function(value, locale) { return (value * hzPerStep).toFixed(1) }
				valueFromText: function(text, locale) { return -Math.round(-Number(text) / hzPerStep) }
				validator: DoubleValidator {
					bottom: Number(cfoValue.textFromValue(cfoValue.from))
					top: Number(cfoValue.textFromValue(cfoValue.to))
					decimals: 1
					notation: DoubleValidator.StandardNotation
					locale: "C"
				}
				implicitWidth: 180
				leftPadding: 8
				rightPadding: 28
				up.indicator: Rectangle {
					x: parent.width - width
					y: 0
					width: 24
					height: parent.height / 2
					color: cfoValue.up.pressed ? "#555555" : cfoValue.up.hovered ? "#444444" : "transparent"
					Text { anchors.centerIn: parent; text: "▴"; color: cfoValue.enabled && cfoValue.value < cfoValue.to ? "white" : "#666666" }
				}
				down.indicator: Rectangle {
					x: parent.width - width
					y: parent.height / 2
					width: 24
					height: parent.height / 2
					color: cfoValue.down.pressed ? "#555555" : cfoValue.down.hovered ? "#444444" : "transparent"
					Text { anchors.centerIn: parent; text: "▾"; color: cfoValue.enabled && cfoValue.value > cfoValue.from ? "white" : "#666666" }
				}
				function isUserActive() { return activeFocus || up.pressed || down.pressed }
			}

			Label { text: "Gain phase"; color: "#ffffff"; horizontalAlignment: Text.AlignRight; Layout.alignment: Qt.AlignRight; Layout.fillWidth: true }
			Switch {
				id: gainPhaseCompensationSwitch
				property string configKey: "gain_phase_compensation"
				property string configProp: "checked"
				Component.onCompleted: poolConfigManager.register(this)
				onCheckedChanged: poolConfigManager.onControlChanged(this)
				checked: true
				ToolTip.visible: root.tooltipsEnabled && hovered
				ToolTip.text: "Compensate deterministic phase jumps when AGC crosses analog gain-element boundaries."
			}

			// Section: Gain
			Label { Layout.columnSpan: 2; text: "Gain"; color: "#9fb3c8" }
			Label { text: "Automatic"; color: "#ffffff"; horizontalAlignment: Text.AlignRight; Layout.alignment: Qt.AlignRight; Layout.fillWidth: true }
			Switch {
				id: gainSwitch
				property string configKey: "gain.automatic"
				property string configProp: "checked"
				property var encode: function(v) { return v ? 1 : 0 }
				property var decode: function(v) { return !!v }
				Component.onCompleted: poolConfigManager.register(this)
				onCheckedChanged: poolConfigManager.onControlChanged(this)
				checked: true
			}

			Label { text: "RX Gain"; color: "#ffffff"; horizontalAlignment: Text.AlignRight; Layout.alignment: Qt.AlignRight; Layout.fillWidth: true }
			RowLayout {
				spacing: 14
				ToolTip.visible: root.tooltipsEnabled && rxGainHover.hovered
				ToolTip.text: root.gainTooltipText
				HoverHandler { id: rxGainHover }
				Slider {
					id: lnaGainSlider
					property string configKey: "gain.rx_gain_value"
					property string configProp: "value"
					property var encode: function(v) { return Math.round(v) }
					property var decode: function(v) { return Math.max(0, Math.min(76, parseInt((v === undefined || v === null || v === "") ? 32 : v))) }
					Component.onCompleted: poolConfigManager.register(this)
					onValueChanged: poolConfigManager.onControlChanged(this)
					from: 0; to: 76; value: 32; stepSize: 1
					implicitWidth: 120
					enabled: !gainSwitch.checked
					function isUserActive() { return pressed }
				}
				Label { text: lnaGainSlider.value; color: "#ffffff" }
			}

			Label { text: "FFT Gain"; color: "#ffffff"; horizontalAlignment: Text.AlignRight; Layout.alignment: Qt.AlignRight; Layout.fillWidth: true }
			RowLayout {
				spacing: 14
				ToolTip.visible: root.tooltipsEnabled && fftGainHover.hovered
				ToolTip.text: root.gainTooltipText
				HoverHandler { id: fftGainHover }
				Slider {
					id: fftGainSlider
					property string configKey: "gain.fft_gain_value"
					property string configProp: "value"
					property var encode: function(v) { return Math.round(v) }
					property var decode: function(v) { return Math.max(-128, Math.min(127, parseInt(v||0))) }
					Component.onCompleted: poolConfigManager.register(this)
					onValueChanged: poolConfigManager.onControlChanged(this)
					from: -128; to: 127; value: 0; stepSize: 1
					implicitWidth: 120
					enabled: !gainSwitch.checked
					function isUserActive() { return pressed }
				}
				Label { text: fftGainSlider.value; color: "#ffffff" }
			}

			// Section MAC filter
			Label { Layout.columnSpan: 2; text: "Firmware MAC Filter"; color: "#9fb3c8" }
			Label { text: "Enable Filter"; color: "#ffffff"; horizontalAlignment: Text.AlignRight; Layout.alignment: Qt.AlignRight; Layout.fillWidth: true }
			Switch {
				id: macFilterSwitch
				property string configKey: "mac_filter.enable"
				property string configProp: "checked"
				property var encode: function(v) { return !!v }
				property var decode: function(v) { return !!v }
				Component.onCompleted: poolConfigManager.register(this)
				onCheckedChanged: poolConfigManager.onControlChanged(this)
				checked: false

				// Only enable MAC when address is valid
				enabled: macAddrInput.isValidMac
			}

			Label { text: "MAC Address"; color: "#ffffff"; horizontalAlignment: Text.AlignRight; Layout.alignment: Qt.AlignRight; Layout.fillWidth: true }
			TextField {
				id: macAddrInput
				property string configKey: "mac_filter.mac_address"
				property string configProp: "text"
				property var encode: function(v) { return v.toString() }
				property var decode: function(v) { return v.toString() }
				property bool isValidMac: false

				Component.onCompleted: poolConfigManager.register(this)

				// Validate while editing
				onTextChanged: {
					isValidMac = poolConfigManager.isValidMacAddress(text)
					if (isValidMac) {
						poolConfigManager.onControlChanged(this)
					} else {
						macFilterSwitch.checked = false
					}
				}

				color: isValidMac ? "#ffffff" : "#ff4d4d"

				Material.accent: isValidMac ? "#227b3d" : "#ff4d4d"

				implicitWidth: 180
				placeholderText: "e.g., 12:34:56:78:9A:BC"
				function isUserActive() { return activeFocus }
			}

			// Spacer
			Rectangle {
				Layout.columnSpan: 2
				width: 1; height: 10
				color: "transparent"
			}

			Button {
				Layout.columnSpan: 2
				Layout.alignment: Qt.AlignCenter
				text: "Reload from Board"
				onClicked: poolConfigManager.action("reload_config")
			}

			Button {
				Layout.columnSpan: 2
				Layout.alignment: Qt.AlignCenter
				text: "Reset to Defaults"
				onClicked: {
					poolConfigManager.action("reset_config")
					// Note: UI will be updated via updateUIState connection
				}
			}

			CheckBox {
				id: disableTooltipsCheckBox
				Layout.columnSpan: 2
				Layout.alignment: Qt.AlignCenter
				text: "Disable tooltips"
				checked: ApplicationWindow.window ? !ApplicationWindow.window.tooltipsEnabled : false
				onToggled: {
					if (ApplicationWindow.window) {
						ApplicationWindow.window.tooltipsEnabled = !checked
					}
				}
			}

			// Spacer
			Rectangle {
				Layout.columnSpan: 2
				width: 1; height: 30
				color: "transparent"
			}
		}
	}

	Common.ConfigManager {
		id: poolConfigManager
		endpoint: poolconfig
	}

	Component.onCompleted: poolConfigManager.fetchAndApply()
}
